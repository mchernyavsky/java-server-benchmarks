allprojects {
    repositories {
        mavenCentral()
    }

    group 'com.github.mchernyavsky'
}

ext.libsVersions = [
        gradle: '4.4.1',
        lombok: '1.16.18',
        slf4j : '1.7.25',
        annotations : '15.0',
        checkstyle : '8.7',
        junit : '4.12'
]
ext.libs = [
        lombok: "org.projectlombok:lombok:${libsVersions.lombok}",
        slf4j : "org.slf4j:slf4j-simple:${libsVersions.slf4j}",
        annotations : "org.jetbrains:annotations:${libsVersions.annotations}",
        junit : "junit:junit:${libsVersions.junit}"
]

subprojects {
    apply plugin: 'java'
    sourceCompatibility = JavaVersion.VERSION_1_8
    test {
        testLogging.showStandardStreams = true
    }

    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion = libsVersions.checkstyle
    }

    tasks.withType(Checkstyle) {
        def configDir = project.rootProject.file('config/checkstyle')
        configProperties = ['config-dir': configDir]
        configFile = new File(configDir, 'checkstyle_checks.xml')
        showViolations = true
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    dependencies {
        compile libs.lombok
        compile libs.slf4j
        compile libs.annotations
        testCompile libs.junit
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = libsVersions.gradle
}
